category IcsLang {
    asset ControlServer extends IcsAsset {
        | access
            +> bruteForceIO,
                defaultCredentials,
                remoteFileCopyReceive,
                roleIdentification
        | authenticatedAccess
            +> dataDestructionIncoming,
                automatedCollection,
                dataFromInformationRepositories,
                modifyParameter,
                monitorProcessState,
                pointAndTackIdentification,
                remoteFileCopySend,
                remoteSystemDiscovery,
                rogueMasterDevice,
                serviceStop

        & automatedCollection
            developer info: "T802"
            -> networks.applications.access

        | bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        & restrictedOperations {I,A}

        | dataDestructionIncoming
            developer info: "T809"
            -> dataDestructionNoAntiMalware,
                dataDestructionBypassAntiMalware
        & dataDestructionNoAntiMalware
            -> dataDestructionDefendedIncoming
        | dataDestructionBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> dataDestructionDefendedIncoming
        | dataDestructionDefendedIncoming
            -> dataDestructionDefended
        & dataDestructionDefended
            developer info: "T809"
            -> dataDestruction
        | dataDestruction {I,A}
            developer info: "T809"

        & dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> credentials

        | externalRemoteService
            developer info: "T822"
            -> access

        | locationIdentification
            -> _locationIdentificationDefended

        & _locationIdentificationDefended @hidden
            developer info: "T825"
            -> access

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact {I}
            developer info: "T836"

        & spoofReportingMessage {I}
            developer info: "T856"

        & monitorProcessState
            developer info: "T801"
            -> networks.applications.access

        & pointAndTackIdentification {C}
            developer info: "T861"

        & remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        & remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.applications.access

        & replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        | rogueMasterDevice
            developer info: "T848"
            -> _rogueMasterDeviceDefended
        & _rogueMasterDeviceDefended @hidden
            developer info: "T848"
            -> networks.applications[IcsAsset].authenticatedAccess

        & roleIdentification
            developer info: "T850"
            -> rogueMasterDevice,
                networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | serviceStop
            developer info: "T881"
            -> dataDestructionIncoming

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801,T856 - Mitigation"
            -> monitorProcessState,
                spoofReportingMessage
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials,
                monitorProcessState,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,T812 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T825,T838,T801,T846,T848,T850,T856 - Mitigation"
            +> dataDestructionDefended,
                _locationIdentificationDefended,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification,
                spoofReportingMessage
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # keepLocationInformationSecret
            developer info: "T825 - Mitigation"
            -> _locationIdentificationDefended
        # applyIDS
            developer info: "T838,T846,T856 - Mitigation"
            -> remoteSystemDiscovery,
                spoofReportingMessage
    }
}