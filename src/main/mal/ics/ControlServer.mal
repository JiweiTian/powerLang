category IcsLang {
    asset ControlServer extends IcsAsset {
        | access
            +> bruteForceIO,
                defaultCredentials,
                remoteFileCopyReceive,
                roleIdentification
        | authenticatedAccess
            +> dataDestructionIncoming,
                dataFromInformationRepositories,
                modifyParameter,
                pointAndTackIdentification,
                remoteFileCopySend,
                rogueMasterDevice,
                serviceStop,
                networks.applications.access

        | bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        & restrictedOperations {I,A}

        | dataDestructionIncoming
            developer info: "T809"
            -> dataDestructionNoAntiMalware,
                dataDestructionBypassAntiMalware
        & dataDestructionNoAntiMalware
            -> dataDestructionDefendedIncoming
        | dataDestructionBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> dataDestructionDefendedIncoming
        | dataDestructionDefendedIncoming
            -> dataDestructionDefended
        & dataDestructionDefended
            developer info: "T809"
            -> dataDestruction
        | dataDestruction {I,A}
            developer info: "T809"

        & dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> credentials

        | externalRemoteService
            developer info: "T822"
            -> access

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact {I}
            developer info: "T836"

        | spoofReportingMessage
            developer info: "T856"
            -> spoofReportingMessageNoIDS,
                spoofReportingMessageBypassIDS
        & spoofReportingMessageNoIDS
            -> spoofReportingMessageDefendedIncoming
        | spoofReportingMessageBypassIDS [Exponential(3.5)]
            modeler info: "Sommestad (2012) Quantifying the effectiveness of intrusion detection systems in operation through domain experts (+ two more)"
            -> spoofReportingMessageDefendedIncoming
        | spoofReportingMessageDefendedIncoming
            -> spoofReportingMessageDefended
        & spoofReportingMessageDefended {I}
            developer info: "T856"

        & pointAndTackIdentification {C}
            developer info: "T861"

        & remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        & remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        & replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        | rogueMasterDevice
            developer info: "T848"
            -> _rogueMasterDeviceDefended
        & _rogueMasterDeviceDefended @hidden
            developer info: "T848"
            -> networks.applications[IcsAsset].authenticatedAccess

        & roleIdentification
            developer info: "T850"
            -> rogueMasterDevice,
                networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | serviceStop
            developer info: "T881"
            -> dataDestructionIncoming

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801,T856 - Mitigation"
            -> spoofReportingMessage
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,T812 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T825,T838,T801,T846,T848,T850,T856 - Mitigation"
            +> dataDestructionDefended,
                roleIdentification,
                spoofReportingMessage
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846,T856 - Mitigation"
            -> spoofReportingMessageNoIDS
    }
}