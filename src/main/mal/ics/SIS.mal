category IcsLang {
    asset SIS extends IcsAsset {
        | access
            +> modifyControlLogic,
                moduleFirmware,
                systemFirmware,
                utilizeChangeOperatingMode,
                dontAlarmSuppression,
                denialOfService,
                dontModifyAlarmSettings,
                modifyControlLogic,
                moduleFirmware,
                monitorProcessState,
                utilizeChangeOperatingMode
        | authenticatedAccess
            +> programDownload,
                activateFirmwareUpdateMode,
                alarmSuppression,
                automatedCollection,
                modifyAlarmSettings,
                programDownload,
                remoteSystemDiscovery
        | restrictedOperations {I,A}
        | physicalImpact {I,A}
            developer info: "T836"
        | hidableAttacks
            -> activateFirmwareUpdateMode,
                automatedCollection,
                modifyControlLogic,
                modifyParameter,
                moduleFirmware,
                programDownload,
                remoteSystemDiscovery,
                systemFirmwareDefended,
                utilizeChangeOperatingMode

        & activateFirmwareUpdateMode
            developer info: "T800"
            -> restrictedOperations

        & alarmSuppression //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontAlarmSuppression //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSuppressionNotModified
            -> alarmSuppression

        & automatedCollection
            developer info: "T802"
            -> networks.applications.access

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        | denialOfService
            developer info: "T814"
            -> restrictedOperations

        | exploitationForEvasion
            developer info: "T820"
            -> systemFirmware

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T838"
            -> modifyAlarmSettings

        | modifyControlLogic
            developer info: "T833"
            -> modifyControlLogicNoAntiMalware,
                modifyControlLogicBypassAntiMalware
        & modifyControlLogicNoAntiMalware
            -> modifyControlLogicDefendedIncoming
        | modifyControlLogicBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> modifyControlLogicDefendedIncoming
        | modifyControlLogicDefendedIncoming
            -> modifyControlLogicDefended
        & modifyControlLogicDefended
            developer info: "T833"
            -> physicalImpact

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & moduleFirmware
            developer info: "T839"
            -> restrictedOperations,
                networks.applications.access,
                authenticatedAccess

        & monitorProcessState
            developer info: "T801"
            -> networks.applications.access

        | programDownload
            developer info: "T843"
            -> physicalImpact,
                restrictedOperations

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.applications.access

        | systemFirmware
            developer info: "T857"
            -> systemFirmwareDefended
        & systemFirmwareDefended
            developer info: "T857"
            -> authenticatedAccess

        & utilizeChangeOperatingMode
            developer info: "T858"
            -> physicalImpact,
                authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPrincipleOfLeastPrivilege
            developer info: "T812,T838,T801,T833,T839,T857,T858 - Mitigation"
            +> defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                modifyControlLogicDefended,
                moduleFirmware,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyMultiFactorAuthentication
            developer info: "T812,T805,T858 - Mitigation"
            +> defaultCredentials,
                utilizeChangeOperatingMode
        # restrictedPhysicalAccess
            developer info: "T838,T801,T846,T805,T833,T839,T857,T858 - Mitigation"
            +> modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                modifyControlLogicDefended,
                moduleFirmware,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # applyIDS
            developer info: "T838,T846,T805,T833,T839,T857 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                modifyControlLogicDefended,
                moduleFirmware,
                systemFirmwareDefended
        # useCryptographyKeyForFirmware
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # whitelistAllowedApplications
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # implementChallengeResponseAuthentication
            developer info: "T858 - Mitigation"
            -> utilizeChangeOperatingMode
    }
}