category System {
    asset SIS extends IcsAsset {
        | access
            +> modifyControlLogic,
                moduleFirmware,
                systemFirmware,
                utilizeChangeOperatingMode
        | authenticatedAccess
            +> programDownload
        | restrictedOperations {I,A}
        | physicalImpact {I,A}
            developer info: "T836"
        | hidableAttacks

        | activateFirmwareUpdateMode
            developer info: "T800"
            -> restrictedOperations

        & alarmSuppression //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontAlarmSuppression //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSuppressionNotModified
            -> alarmSuppression

        | automatedCollection
            developer info: "T802"
            -> networks.assets.access

        & blockSerialCom
            developer info: "T805"
            -> restrictedOperations

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        | denialOfService
            developer info: "T814"
            -> restrictedOperations

        | exploitationForEvasion
            developer info: "T820"
            -> systemFirmware

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T838"
            -> modifyAlarmSettings

        & modifyControlLogic
            developer info: "T833"
            -> physicalImpact

        & moduleFirmware
            developer info: "T839"
            -> restrictedOperations,
                networks.assets.access,
                authenticatedAccess

        & monitorProcessState
            developer info: "T801"
            -> networks.assets.access

        | programDownload
            developer info: "T843"
            -> physicalImpact,
                restrictedOperations

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access

        & systemFirmware
            developer info: "T857"
            -> authenticatedAccess

        & utilizeChangeOperatingMode
            developer info: "T858"
            -> physicalImpact,
                authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPrincipleOfLeastPrivilege
            developer info: "T812,T838,T801,T833,T839,T857,T858 - Mitigation"
            -> defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                modifyControlLogic,
                moduleFirmware,
                systemFirmware,
                utilizeChangeOperatingMode
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyMultiFactorAuthentication
            developer info: "T812,T805,T858 - Mitigation"
            -> defaultCredentials,
                blockSerialCom,
                utilizeChangeOperatingMode
        # restrictedPhysicalAccess
            developer info: "T838,T801,T846,T805,T833,T839,T857,T858 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                blockSerialCom,
                modifyControlLogic,
                moduleFirmware,
                systemFirmware,
                utilizeChangeOperatingMode
        # applyIDS
            developer info: "T838,T846,T805,T833,T839,T857 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                blockSerialCom,
                modifyControlLogic,
                moduleFirmware,
                systemFirmware
        # useCryptographyKeyForFirmware
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # whitelistAllowedApplications
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # implementChallengeResponseAuthentication
            developer info: "T858 - Mitigation"
            -> utilizeChangeOperatingMode
    }
}