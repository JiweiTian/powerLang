category IcsLang {
    asset EngineeringWorkstation extends IcsAsset {
        | access
            +> defaultCredentials,
                scripting,
                noLossOfView
        | authenticatedAccess
            +> dataFromInformationRepositories,
                exploitationOfRemoteService,
                lossOfView,
                manipulationOfView,
                projectFileInfection,
                serviceStop,
                rogueMasterDevice,
                modifyControlLogic
        | hidableAttacks
            -> exploitationOfRemoteService,
                projectFileInfection,
                rogueMasterDevice,
                scripting,
                serviceStop,
                userExecution,
                modifyControlLogic
        | physicalImpact {I}
        | dataDestruction {I,A}

        | dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> credentials

        & exploitationOfRemoteService
            developer info: "T866"
            -> networks.applications.access

        & lossOfView //lowProbability
            developer info: "T829"
            -> hidableAttacks
        | noLossOfView //highProbability
            developer info: "T829"
            -> hidableAttacks
        # lossOfViewNotInPlace
            developer info: "T829"
            -> lossOfView

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        & projectFileInfection
            developer info: "T873"
            -> networks.applications[Controller].authenticatedAccess

        | rogueMasterDevice
            developer info: "T848"
            -> _rogueMasterDeviceDefended
        & _rogueMasterDeviceDefended @hidden
            developer info: "T848"
            -> networks.applications[IcsAsset].authenticatedAccess

        & scripting
            developer info: "T853"
            -> authenticatedAccess

        & serviceStop
            developer info: "T881"
            -> dataDestruction

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        & userExecution
            developer info: "T863"
            -> authenticatedAccess

        | modifyControlLogic
            developer info: "T833"
            -> networks.applications[Controller].modifyControlLogic,
                networks.applications[SIS].modifyControlLogic

        # applyPrincipleOfLeastPrivilege
            developer info: "T812,T853 - Mitigation"
            -> defaultCredentials,
                scripting
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyMultiFactorAuthentication
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T848,T853 - Mitigation"
            -> _rogueMasterDeviceDefended,
                scripting
        # applyPasswordsToAllowChanges
            developer info: "T853 - Mitigation"
            -> scripting
    }
}