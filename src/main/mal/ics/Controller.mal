category System {
    asset Controller extends IcsAsset {
        | access
            +> bruteForceIO,
                dontAlarmSuppression,
                blockSerialCom,
                changeProgramState,
                denialOfService,
                detectOperatingMode,
                detectProgramState,
                executionThroughApi
        | authenticatedAccess
            +> dataDestruction,
                activateFirmwareUpdateMode,
                alarmSuppression,
                automatedCollection,
                deviceRestartShutdown
        | hidableAttacks
        | restrictedOperations {I,A}
        | physicalImpact
            developer info: "T836"

        & dataDestruction {I,A}
            developer info: "T809"

        | activateFirmwareUpdateMode
            developer info: "T800"
            -> restrictedOperations

        & alarmSuppression //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontAlarmSuppression //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSuppressionNotModified
            -> alarmSuppression

        | automatedCollection
            developer info: "T802"
            -> networks.assets.access

        & blockReportingMessage
            developer info: "T804"
            -> restrictedOperations

        & blockSerialCom
            developer info: "T805"
            -> restrictedOperations

        | bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        | changeProgramState
            developer info: "T875"
            -> authenticatedAccess,
                deviceRestartShutdown

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        | denialOfService
            developer info: "T814"
            -> restrictedOperations

        | detectOperatingMode
            developer info: "T868"
            -> authenticatedAccess

        | detectProgramState
            developer info: "T870"
            -> authenticatedAccess

        | deviceRestartShutdown
            developer info: "T816"
            -> deviceRestartShutdownDefended
        & deviceRestartShutdownDefended
            -> physicalImpact

        | executionThroughApi
            developer info: "T871"
            -> changeProgramState

        | exploitationForEvasion
            developer info: "T820"
            -> systemFirmware

        | IOimage
            developer info: "T877"
            //no clue

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & unauthorizedCommandMessage
            developer info: "T855"

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            -> modifyAlarmSettings
        & monitorProcessState
            developer info: "T801"
            -> networks.assets.access
        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access
        & roleIdentification
            developer info: "T850"
            -> networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice

        | systemFirmware

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850,T816 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification,
                deviceRestartShutdownDefended
        # applyMultiFactorAuthentication
            developer info: "T809,T812,T805,T816 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                blockSerialCom,
                deviceRestartShutdownDefended
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T850,T805,T816 - Mitigation"
            -> dataDestruction,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification,
                blockSerialCom,
                deviceRestartShutdownDefended
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846,T805,T816 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                blockSerialCom,
                deviceRestartShutdownDefended
    }
}