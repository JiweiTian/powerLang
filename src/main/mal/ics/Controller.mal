category System {
    asset Controller extends IcsAsset {
        | access
            +> bruteForceIO,
                dontAlarmSuppression,
                blockSerialCom,
                changeProgramState,
                denialOfService,
                detectOperatingMode,
                detectProgramState,
                executionThroughApi,
                IOmoduleDiscovery,
                modifyControlLogic,
                moduleFirmware,
                dontModifyAlarmSettings,
                monitorProcessState,
                roleIdentification,
                noRootkit,
                systemFirmware,
                utilizeChangeOperatingMode
        | authenticatedAccess
            +> dataDestruction,
                activateFirmwareUpdateMode,
                alarmSuppression,
                automatedCollection,
                deviceRestartShutdown,
                memoryManipulation,
                overrideTestingFunction,
                manipulationOfView,
                modifyAlarmSettings,
                programDownload,
                remoteSystemDiscovery,
                rootkit
        | hidableAttacks
            -> activateFirmwareUpdateMode,
                automatedCollection,
                blockReportingMessage,
                blockSerialCom,
                bruteForceIO,
                changeProgramState,
                modifyControlLogic,
                modifyParameter,
                moduleFirmware,
                programDownload,
                remoteSystemDiscovery,
                systemFirmwareDefended,
                unauthorizedCommandMessage,
                utilizeChangeOperatingMode
        | restrictedOperations {I,A}
        | physicalImpact {I,A}
            developer info: "T836"

        & dataDestruction {I,A}
            developer info: "T809"

        & activateFirmwareUpdateMode
            developer info: "T800"
            -> restrictedOperations

        & alarmSuppression //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontAlarmSuppression //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSuppressionNotModified
            -> alarmSuppression

        & automatedCollection
            developer info: "T802"
            -> networks.assets.access

        & blockReportingMessage
            developer info: "T804"
            -> restrictedOperations

        & blockSerialCom
            developer info: "T805"
            -> restrictedOperations

        & bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        & changeProgramState
            developer info: "T875"
            -> authenticatedAccess,
                deviceRestartShutdown

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        | denialOfService
            developer info: "T814"
            -> restrictedOperations

        | detectOperatingMode
            developer info: "T868"
            -> authenticatedAccess

        | detectProgramState
            developer info: "T870"
            -> authenticatedAccess

        | deviceRestartShutdown
            developer info: "T816"
            -> deviceRestartShutdownDefended
        & deviceRestartShutdownDefended
            -> physicalImpact

        | executionThroughApi
            developer info: "T871"
            -> changeProgramState

        | exploitationForEvasion
            developer info: "T820"
            -> systemFirmware

        | IOimage
            developer info: "T877"
            -> manipulateIOimage

        & IOmoduleDiscovery
            developer info: "T824"
            -> IOimage,
                networks.assets.access

        & manipulateIOimage
            developer info: "T835"
            -> hidableAttacks,
                physicalImpact
        | memoryManipulation
            -> manipulateIOimage
        | overrideTestingFunction
            -> manipulateIOimage

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            -> modifyAlarmSettings

        & modifyControlLogic
            developer info: "T833"
            -> physicalImpact

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & moduleFirmware
            developer info: "T839"
            -> restrictedOperations,
                networks.assets.access,
                authenticatedAccess

        & monitorProcessState
            developer info: "T801"
            -> networks.assets.access

        & programDownload
            developer info: "T843"
            -> physicalImpact,
                restrictedOperations

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access

        & roleIdentification
            developer info: "T850"
            -> networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice

        & rootkit //lowProbability
            developer info: "T851"
            -> hidableAttacks,
                physicalImpact,
                restrictedOperations
        | noRootkit //highProbabilty
            developer info: "T851"
            -> hidableAttacks
        # rootkitNotInPlace
            -> rootkit

        | systemFirmwareDefended
            developer info: "T857"
            ->systemFirmwareDefended
        & systemFirmwareDefended
            developer info: "T857"
            -> authenticatedAccess

        & unauthorizedCommandMessage
            developer info: "T855"
            -> physicalImpact

        & utilizeChangeOperatingMode
            developer info: "T858"
            -> physicalImpact,
                authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850,T816,T833,T839,T857,T858 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification,
                deviceRestartShutdownDefended,
                modifyControlLogic,
                moduleFirmware,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # useCryptographyKeyForFirmware
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # applyMultiFactorAuthentication
            developer info: "T809,T812,T805,T816,T824,T851,T858 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                blockSerialCom,
                deviceRestartShutdownDefended,
                IOmoduleDiscovery,
                rootkit,
                utilizeChangeOperatingMode
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T850,T805,T816,T824,T833,T839,T851,T857,T858 - Mitigation"
            -> dataDestruction,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification,
                blockSerialCom,
                deviceRestartShutdownDefended,
                IOmoduleDiscovery,
                modifyControlLogic,
                moduleFirmware,
                rootkit,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # whitelistAllowedApplications
            developer info: "T809,T839,T851 - Mitigation"
            -> dataDestruction,
                moduleFirmware,
                rootkit
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846,T805,T816,T833,T839,T857,T855 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                blockSerialCom,
                deviceRestartShutdownDefended,
                modifyControlLogic,
                moduleFirmware,
                systemFirmwareDefended,
                unauthorizedCommandMessage
        # implementChallengeResponseAuthentication
            developer info: "T858 - Mitigation"
            -> utilizeChangeOperatingMode
    }
}