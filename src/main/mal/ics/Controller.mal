category IcsLang {
    asset Controller extends IcsAsset {
        | access
            +> bruteForceIO,
                blockSerialCom,
                changeProgramState,
                denialOfService,
                detectOperatingMode,
                detectProgramState,
                executionThroughApi,
                IOmoduleDiscovery,
                modifyControlLogic,
                moduleFirmware,
                monitorProcessState,
                roleIdentification,
                systemFirmware,
                utilizeChangeOperatingMode
        | authenticatedAccess
            +> dataDestructionIncoming,
                activateFirmwareUpdateMode,
                automatedCollection,
                deviceRestartShutdown,
                memoryManipulation,
                overrideTestingFunction,
                manipulationOfView,
                programDownload,
                remoteSystemDiscovery,
                rootkit
        | restrictedOperations {I,A}
        | physicalImpact {I,A}
            developer info: "T836"

        | dataDestructionIncoming
            developer info: "T809"
            -> dataDestructionNoAntiMalware,
                dataDestructionBypassAntiMalware
        & dataDestructionNoAntiMalware
            -> dataDestructionDefendedIncoming
        | dataDestructionBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> dataDestructionDefendedIncoming
        | dataDestructionDefendedIncoming
            -> dataDestructionDefended
        & dataDestructionDefended
            developer info: "T809"
            -> dataDestruction
        | dataDestruction {I,A}
            developer info: "T809"

        & activateFirmwareUpdateMode
            developer info: "T800"
            -> restrictedOperations

        & automatedCollection
            developer info: "T802"
            -> networks.applications.access

        & blockReportingMessage
            developer info: "T804"
            -> restrictedOperations

        & blockSerialCom
            developer info: "T805"
            -> restrictedOperations

        & bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        & changeProgramState
            developer info: "T875"
            -> authenticatedAccess,
                deviceRestartShutdown

        | defaultCredentials
            -> _defaultCredentialsDefended
        & _defaultCredentialsDefended @hidden
            developer info: "T812"
            -> authenticatedAccess

        | denialOfService
            developer info: "T814"
            -> restrictedOperations

        | detectOperatingMode
            developer info: "T868"
            -> authenticatedAccess

        | detectProgramState
            developer info: "T870"
            -> authenticatedAccess

        | deviceRestartShutdown
            developer info: "T816"
            -> deviceRestartShutdownDefended
        & deviceRestartShutdownDefended
            -> physicalImpact

        | executionThroughApi
            developer info: "T871"
            -> changeProgramState

        | IOimage
            developer info: "T877"
            -> manipulateIOimage

        & IOmoduleDiscovery
            developer info: "T824"
            -> IOimage,
                networks.applications.access

        & manipulateIOimage
            developer info: "T835"
            -> //hidableAttacks,
                physicalImpact
        | memoryManipulation
            -> manipulateIOimage
        | overrideTestingFunction
            -> manipulateIOimage

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        | modifyControlLogic
            developer info: "T833"
            -> modifyControlLogicNoAntiMalware,
                modifyControlLogicBypassAntiMalware
        & modifyControlLogicNoAntiMalware
            -> modifyControlLogicDefendedIncoming
        | modifyControlLogicBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> modifyControlLogicDefendedIncoming
        | modifyControlLogicDefendedIncoming
            -> modifyControlLogicDefended
        & modifyControlLogicDefended
            developer info: "T833"
            -> physicalImpact

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & moduleFirmware
            developer info: "T839"
            -> restrictedOperations,
                networks.applications.access,
                authenticatedAccess

        & monitorProcessState
            developer info: "T801"
            -> networks.applications.access

        & programDownload
            developer info: "T843"
            -> physicalImpact,
                restrictedOperations

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.applications.access

        & roleIdentification
            developer info: "T850"
            -> networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | rootkit
            developer info: "T859"
            -> rootkitNoAntiMalware,
                rootkitBypassAntiMalware
        & rootkitNoAntiMalware
            -> rootkitDefendedIncoming
        | rootkitBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> rootkitDefendedIncoming
        | rootkitDefendedIncoming
            -> rootkitDefended
        & rootkitDefended
            developer info: "T851"
            -> physicalImpact,
                restrictedOperations

        | systemFirmware
            developer info: "T857"
            ->systemFirmwareDefended
        & systemFirmwareDefended
            developer info: "T857"
            -> authenticatedAccess

        & unauthorizedCommandMessage
            developer info: "T855"
            -> physicalImpact

        & utilizeChangeOperatingMode
            developer info: "T858"
            -> physicalImpact,
                authenticatedAccess

        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850,T816,T833,T839,T857,T858 - Mitigation"
            +> dataDestructionDefended,
                _defaultCredentialsDefended,
                monitorProcessState,
                roleIdentification,
                deviceRestartShutdownDefended,
                modifyControlLogicDefended,
                moduleFirmware,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # useCryptographyKeyForFirmware
            developer info: "T839 - Mitigation"
            -> moduleFirmware
        # applyMultiFactorAuthentication
            developer info: "T809,T812,T805,T816,T824,T851,T858 - Mitigation"
            +> dataDestructionDefended,
                _defaultCredentialsDefended,
                blockSerialCom,
                deviceRestartShutdownDefended,
                IOmoduleDiscovery,
                rootkitDefended,
                utilizeChangeOperatingMode
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T850,T805,T816,T824,T833,T839,T851,T857,T858 - Mitigation"
            +> dataDestructionDefended,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification,
                blockSerialCom,
                deviceRestartShutdownDefended,
                IOmoduleDiscovery,
                modifyControlLogicDefended,
                moduleFirmware,
                rootkitDefended,
                systemFirmwareDefended,
                utilizeChangeOperatingMode
        # whitelistAllowedApplications
            developer info: "T809,T839,T851 - Mitigation"
            -> dataDestructionDefended,
                moduleFirmware,
                rootkitDefended
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> _defaultCredentialsDefended
        # applyIDS
            developer info: "T838,T846,T805,T816,T833,T839,T857,T855 - Mitigation"
            -> remoteSystemDiscovery,
                blockSerialCom,
                deviceRestartShutdownDefended,
                modifyControlLogicDefended,
                moduleFirmware,
                systemFirmwareDefended,
                unauthorizedCommandMessage
        # implementChallengeResponseAuthentication
            developer info: "T858 - Mitigation"
            -> utilizeChangeOperatingMode
    }
}