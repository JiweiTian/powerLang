category IcsLang {
    asset DataHistorian extends IcsAsset {
        | access
            +> remoteFileCopyReceive,
                roleIdentification
        | authenticatedAccess
            +> dataFromInformationRepositories,
                pointAndTackIdentification,
                remoteFileCopySend,
                serviceStop,
                networks.applications.access

        | dataFromInformationRepositories {C}
            developer info: "T811"

        | pointAndTackIdentification {C}
            developer info: "T861"

        | remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        | remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        & roleIdentification
            developer info: "T850"
            -> networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | serviceStop
            developer info: "T881"

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # restrictedPhysicalAccess
            developer info: "T801,T846,T850 - Mitigation"
            +> roleIdentification
        # applyPrincipleOfLeastPrivilege
            developer info: "T801,T850 - Mitigation"
            +> roleIdentification
    }
}