category IcsLang {
    asset DataHistorian extends IcsAsset {
        | access
            +> remoteFileCopyReceive,
                roleIdentification
        | authenticatedAccess
            +> dataFromInformationRepositories,
                exploitationOfRemoteService,
                monitorProcessState,
                pointAndTackIdentification,
                remoteFileCopySend,
                remoteSystemDiscovery,
                serviceStop

        | dataFromInformationRepositories {C}
            developer info: "T811"

        | exploitationOfRemoteService
            developer info: "T866"
            -> networks.applications.access

        & monitorProcessState
            developer info: "T801"
            -> networks.applications.access

        | pointAndTackIdentification {C}
            developer info: "T861"

        | remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        | remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.applications.access

        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        & roleIdentification
            developer info: "T850"
            -> networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | serviceStop
            developer info: "T881"

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # restrictedPhysicalAccess
            developer info: "T801,T846,T850 - Mitigation"
            -> monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification
        # applyPrincipleOfLeastPrivilege
            developer info: "T801,T850 - Mitigation"
            -> monitorProcessState,
                roleIdentification
        # regularAuditOfSystem
            developer info: "T801 - Mitigation"
            -> monitorProcessState
        # applyIDS
            developer info: "T846 - Mitigation"
            -> remoteSystemDiscovery
    }
}