category System {
    asset HMI extends IcsAsset {
        | access
            +> defaultCredentials,
                noLossOfView,
                dontMasquerade,
                dontModifyAlarmSettings,
                monitorProcessState,
                remoteFileCopyReceive,
                roleIdentification,
                screenCapture
        | authenticatedAccess
            +> dataDestruction,
                dataFromInformationRepositories,
                exploitationOfRemoteService,
                lossOfView,
                manipulationOfView,
                masquerade,
                modifyAlarmSettings,
                modifyParameter,
                networkConnectionEnumeration,
                projectFileInfection,
                remoteFileCopySend,
                remoteSystemDiscovery,
                rogueMasterDevice,
                serviceStop
        | hidableAttacks
            -> dataDestructionDefended,
                dataFromInformationRepositories,
                exploitationOfRemoteService,
                manipulationOfView,
                modifyParameter,
                monitorProcessState,
                networkConnectionEnumeration,
                pointAndTackIdentification,
                projectFileInfection,
                remoteFileCopyReceive,
                remoteFileCopySend,
                remoteSystemDiscovery,
                rogueMasterDeviceDefended,
                roleIdentification,
                serviceStop

        | dataDestruction
            developer info: "T809"
        & dataDestructionDefended {I,A}
            developer info: "T809"

        | physicalImpact
            developer info: "T836"

        & dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        & exploitationOfRemoteService
            developer info: "T866"
            -> networks.applications.access

        | graphicalUserInterface
            developer info: "T823"
            -> access

        & lossOfView //lowProbability
            developer info: "T829"
            -> hidableAttacks
        | noLossOfView //highProbability
            developer info: "T829"
            -> hidableAttacks
        # lossOfViewNotInPlace
            developer info: "T829"
            -> lossOfView

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        & masquerade //lowProbability
            developer info: "T849"
            -> hidableAttacks
        | dontMasquerade //highProbablity
            developer info: "T849"
            -> hidableAttacks
        # masqueradeInPlace
            developer info: "T849"
            -> masquerade

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T838"
            -> modifyAlarmSettings

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & monitorProcessState
            developer info: "T801"
            -> networks.applications.access

        & networkConnectionEnumeration
            developer info: "T840"
            -> networks.applications.access

        & pointAndTackIdentification {C}
            developer info: "T861"

        & projectFileInfection
            developer info: "T873"
            -> networks.applications[Controller].authenticatedAccess

        & remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        & remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.applications.access

        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        | rogueMasterDevice
            developer info: "T848"
            -> rogueMasterDeviceDefended
        & rogueMasterDeviceDefended
            developer info: "T848"
            -> networks.applications[IcsAsset].authenticatedAccess

        & roleIdentification
            developer info: "T850"
            -> networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | screenCapture {C}
            developer info: "T852"

        & serviceStop
            developer info: "T881"
            -> dataDestruction

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPasswordAuthentication
            developer info: "T809,T823 - Mitigation"
            -> dataDestructionDefended,
                graphicalUserInterface
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            -> dataDestructionDefended,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,812 - Mitigation"
            -> dataDestructionDefended,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T848,T850,T823,T840 - Mitigation"
            -> dataDestructionDefended,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                rogueMasterDeviceDefended,
                roleIdentification,
                graphicalUserInterface,
                networkConnectionEnumeration
        # whitelistAllowedApplications
            developer info: "T809,T823 - Mitigation"
            -> dataDestructionDefended,
                graphicalUserInterface
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846,T840 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                networkConnectionEnumeration
    }
}