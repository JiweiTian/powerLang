category Ics {
    asset HMI extends IcsAsset {
        | access
            +> defaultCredentials,
                remoteFileCopyReceive,
                roleIdentification,
                pointAndTackIdentification,
                screenCapture
        | authenticatedAccess
            +> dataDestructionIncoming,
                dataFromInformationRepositories,
                manipulationOfView,
                modifyParameter,
                projectFileInfection,
                remoteFileCopySend,
                rogueMasterDevice,
                serviceStop,
                networks.applications.access

        | dataDestructionIncoming
            developer info: "T809"
            -> dataDestructionNoAntiMalware,
                dataDestructionBypassAntiMalware
        & dataDestructionNoAntiMalware
            -> dataDestructionDefendedIncoming
        | dataDestructionBypassAntiMalware [Bernoulli(0.9)]
            modeler info: "Bagget (2008) Effectiveness of antivirus in detecting metasploit payloads"
            -> dataDestructionDefendedIncoming
        | dataDestructionDefendedIncoming
            -> dataDestructionDefended
        & dataDestructionDefended
            developer info: "T809"
            -> dataDestruction
        | dataDestruction {I,A}
            developer info: "T809"

        | physicalImpact
            developer info: "T836"

        & dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess

        | graphicalUserInterface
            -> _graphicalUserInterfaceDefended

        & _graphicalUserInterfaceDefended @hidden
            developer info: "T823"
            -> access

        | manipulationOfView
            developer info: "T832"
            -> physicalImpact

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact

        & pointAndTackIdentification {C}
            developer info: "T861"

        & projectFileInfection
            developer info: "T873"
            -> networks.applications[Controller].authenticatedAccess

        & remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        & remoteFileCopySend
            developer info: "T867"
            -> networks.applications[ControlServer].remoteFileCopyReceive,
                networks.applications[HMI].remoteFileCopyReceive,
                networks.applications[DataHistorian].remoteFileCopyReceive

        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        | rogueMasterDevice
            developer info: "T848"
            -> _rogueMasterDeviceDefended
        & _rogueMasterDeviceDefended @hidden
            developer info: "T848"
            -> networks.applications[IcsAsset].authenticatedAccess

        & roleIdentification
            developer info: "T850"
            -> networks.applications[ControlServer].rogueMasterDevice,
                networks.applications[HMI].rogueMasterDevice,
                networks.applications[EngineeringWorkstation].rogueMasterDevice

        | screenCapture {C}
            developer info: "T852"

        & serviceStop
            developer info: "T881"
            -> dataDestructionIncoming

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # applyPasswordAuthentication
            developer info: "T809,T823 - Mitigation"
            -> dataDestructionDefended,
                _graphicalUserInterfaceDefended
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,812 - Mitigation"
            +> dataDestructionDefended,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T848,T850,T823,T840 - Mitigation"
            +> dataDestructionDefended,
                _rogueMasterDeviceDefended,
                roleIdentification,
                _graphicalUserInterfaceDefended
        # whitelistAllowedApplications
            developer info: "T809,T823 - Mitigation"
            -> dataDestructionDefended,
                _graphicalUserInterfaceDefended
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
    }
}