category System {
    abstract asset IcsAsset {
        | access
            -> authenticatedAccess
        & authenticatedAccess
        | stealCredentials
            developer info: "T859"
            -> credentials
        | credentials
            -> validAccounts
        & validAccounts
            developer info: "T859"
            -> authenticatedAccess

        # applyPrincipleOfLeastPrivilege
            developer info: "T859 - Mitigation"
            -> validAccounts
        # applyMultiFactorAuthentication
            developer info: "T859 - Mitigation"
            -> validAccounts
        # restrictedPhysicalAccess
            developer info: "T859 - Mitigation"
            -> validAccounts
        # restrictedAccessToConfigurationSettings
            developer info: "T885 - Mitigation"
            -> networks.assets[Firewall].commonlyUsedPort
        # restrictiveSettings
            developer info: "T885 - Mitigation"
            -> networks.assets[Firewall].commonlyUsedPort
        # assessedAndSecured
            developer info: "T885 - Mitigation"
            -> networks.assets[Firewall].commonlyUsedPort
    }

    asset ControlServer extends IcsAsset {
        | access
            +> bruteForceIO,
                defaultCredentials,
                remoteFileCopyReceive,
                roleIdentification,
                hidableAttacks
        & authenticatedAccess
            +> dataDestruction,
                automatedCollection,
                dataFromInformationRepositories,
                modifyParameter,
                masquerade,
                dontMasquerade,
                modifyAlarmSettings,
                dontModifyAlarmSettings,
                monitorProcessState,
                pointAndTackIdentification,
                remoteFileCopySend,
                remoteSystemDiscovery,
                rogueMasterDevice,
                serviceStop
        | hidableAttacks
            -> automatedCollection,
                restrictedOperations,
                dataDestructionDefended,
                dataFromInformationRepositories,
                modifyParameter,
                spoofReportingMessage,
                monitorProcessState,
                pointAndTackIdentification,
                remoteFileCopyReceive,
                remoteFileCopySend,
                remoteSystemDiscovery,
                replicationThroughRemovableMedia

        & automatedCollection
            developer info: "T802"
            -> networks.assets.access

        | bruteForceIO
            developer info: "T806"
            -> restrictedOperations

        & restrictedOperations {I,A}

        | dataDestruction
            developer info: "T809"
            -> dataDestructionDefended
        & dataDestructionDefended {I,A}
            developer info: "T809"

        & dataFromInformationRepositories {C}
            developer info: "T811"

        & defaultCredentials
            developer info: "T812"
            -> credentials

        | externalRemoteService
            developer info: "T822"
            -> access

        & locationIdentification
            developer info: "T825"
            -> access

        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact {I}
            developer info: "T836"

        & spoofReportingMessage {I}
            developer info: "T856"

        & masquerade //lowProbability
            developer info: "T849"
            -> hidableAttacks
        | dontMasquerade //highProbablity
            developer info: "T849"
            -> hidableAttacks
        # masqueradeNotInPlace
            developer info: "T849"
            -> hidableAttacks

        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T838"
            -> modifyAlarmSettings

        & monitorProcessState
            developer info: "T801"
            -> networks.assets.access

        & pointAndTackIdentification {C}
            developer info: "T861"

        & remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        & remoteFileCopySend
            developer info: "T867"
            -> networks.assets[ControlServer].remoteFileCopyReceive,
                networks.assets[HMI].remoteFileCopyReceive,
                networks.assets[DataHistorian].remoteFileCopyReceive

        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access

        & replicationThroughRemovableMedia
            developer info: "T847"
            -> access

        | rogueMasterDevice
            developer info: "T848"
            -> rogueMasterDeviceHided
        & rogueMasterDeviceHided
            developer info: "T848"
            -> networks.assets.authenticatedAccess

        & roleIdentification
            developer info: "T850"
            -> rogueMasterDevice,
                networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice

        | serviceStop
            developer info: "T881"
            -> dataDestruction

        | spearfishingAttachment
            developer info: "T865"
            -> userExecution

        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801,T856 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState,
                spoofReportingMessage
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            -> dataDestructionDefended,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,T812 - Mitigation"
            -> dataDestructionDefended,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T825,T838,T801,T846,T848,T850,T856 - Mitigation"
            -> dataDestructionDefended,
                locationIdentification,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification,
                spoofReportingMessage
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestructionDefended
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # keepLocationInformationSecret
            developer info: "T825 - Mitigation"
            -> locationIdentification
        # applyIDS
            developer info: "T838,T846,T856 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery,
                spoofReportingMessage
    }

    asset EngineeringWorkstation extends IcsAsset {
        | dataFromInformationRepositories {C}
            developer info: "T811"
        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess
        & rogueMasterDevice
            developer info: "T848"
            -> networks.assets.authenticatedAccess
        | serviceStop
            developer info: "T881"
        | spearfishingAttachment
            developer info: "T865"
            -> userExecution
        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812 - Mitigation"
            -> defaultCredentials
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyMultiFactorAuthentication
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T848 - Mitigation"
            -> rogueMasterDevice
    }

    asset AntiVirus {
        # maliciousFileDetectionActivated
            developer info: "T809,T859 - Mitigation"
            -> observed[ControlServer].dataDestruction,
                observed[Controller].dataDestruction,
                observed[HMI].dataDestruction,
                observed.validAccounts
    }

    asset Firewall extends IcsAsset {
        | access
           +> commonlyUsedPort
        & commonlyUsedPort
            developer info: "T885"
            -> bypass
        | bypass
            -> networks.assets.access
        # applyWhitelist
            developer info: "T885 - Mitigation"
            -> commonlyUsedPort
        # justAllowVPN
            developer info: "T885 - Mitigation"
            -> commonlyUsedPort
        # applyIDS
            developer info: "T885 - Mitigation"
            -> commonlyUsedPort
    }

    asset Controller extends IcsAsset {
        | access
            +> bruteForceIO
        & authenticatedAccess
            +> dataDestruction
        | automatedCollection
            developer info: "T802"
            -> networks.assets.access
        | bruteForceIO
            developer info: "T806"
            -> restrictedOperations
        | restrictedOperations {I,A}
        & dataDestruction {I,A}
            developer info: "T809"
        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess
        & blockReportingMessage
            developer info: "T804"
        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact
            developer info: "T836"
        & unauthorizedCommandMessage
            developer info: "T855"
        | hidableAttacks
        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            -> modifyAlarmSettings
        & monitorProcessState
            developer info: "T801"
        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access
        & roleIdentification
            developer info: "T850"
            -> networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,812 - Mitigation"
            -> dataDestruction,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T850 - Mitigation"
            -> dataDestruction,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery
    }

    asset DataHistorian extends IcsAsset {
        | dataFromInformationRepositories {C}
            developer info: "T811"
        & monitorProcessState
            developer info: "T801"
        | pointAndTackIdentification
            developer info: "T861"
        | remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        | remoteFileCopySend
            developer info: "T867"
            -> networks.assets[ControlServer].remoteFileCopyReceive,
                networks.assets[HMI].remoteFileCopyReceive,
                networks.assets[DataHistorian].remoteFileCopyReceive
        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access
        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access
        & roleIdentification
            developer info: "T850"
            -> networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice
        | serviceStop
            developer info: "T881"
        | spearfishingAttachment
            developer info: "T865"
            -> userExecution
        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # restrictedPhysicalAccess
            developer info: "T801,T846,T850 - Mitigation"
            -> monitorProcessState,
                remoteSystemDiscovery,
                roleIdentification
        # applyPrincipleOfLeastPrivilege
            developer info: "T801,T850 - Mitigation"
            -> monitorProcessState,
                roleIdentification
        # regularAuditOfSystem
            developer info: "T801 - Mitigation"
            -> monitorProcessState
        # applyIDS
            developer info: "T846 - Mitigation"
            -> remoteSystemDiscovery
    }

    asset HMI extends IcsAsset {
        & authenticatedAccess
            +> dataDestruction
        & dataDestruction {I,A}
            developer info: "T809"
        | dataFromInformationRepositories {C}
            developer info: "T811"
        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess
        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact
            developer info: "T836"
        & masquerade //lowProbability
            developer info: "T849"
            -> hidableAttacks
        | dontMasquerade //highProbablity
            developer info: "T849"
            -> hidableAttacks
        # masqueradeInPlace
            developer info: "T849"
            -> hidableAttacks
        | hidableAttacks
            //add attacks
        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T801"
            -> modifyAlarmSettings
        & monitorProcessState
            developer info: "T801"
        | pointAndTackIdentification
            developer info: "T861"
        | remoteFileCopyReceive
            developer info: "T867"
            -> authenticatedAccess
        | remoteFileCopySend
            developer info: "T867"
            -> networks.assets[ControlServer].remoteFileCopyReceive,
                networks.assets[HMI].remoteFileCopyReceive,
                networks.assets[DataHistorian].remoteFileCopyReceive
        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access
        | replicationThroughRemovableMedia
            developer info: "T847"
            -> access
        & rogueMasterDevice
            developer info: "T848"
            -> networks.assets.authenticatedAccess
        & roleIdentification
            developer info: "T850"
            -> networks.assets[ControlServer].rogueMasterDevice,
                networks.assets[HMI].rogueMasterDevice,
                networks.assets[EngineeringWorkstation].rogueMasterDevice
        | serviceStop
            developer info: "T881"
            -> dataDestruction
        | spearfishingAttachment
            developer info: "T865"
            -> userExecution
        | userExecution
            developer info: "T863"
            -> authenticatedAccess

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPasswordAuthentication
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPrincipleOfLeastPrivilege
            developer info: "T809,T812,T838,T801,T850 - Mitigation"
            -> dataDestruction,
                defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState,
                roleIdentification
        # applyMultiFactorAuthentication
            developer info: "T809,812 - Mitigation"
            -> dataDestruction,
                defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T809,T838,T801,T846,T848,T850 - Mitigation"
            -> dataDestruction,
                modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery,
                rogueMasterDevice,
                roleIdentification
        # whitelistAllowedApplications
            developer info: "T809 - Mitigation"
            -> dataDestruction
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyIDS
            developer info: "T838,T846 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery
    }

    asset IOServer extends IcsAsset {
        | externalRemoteService
            developer info: "T822"
            -> access
        & blockReportingMessage
            developer info: "T804"
    }

    asset SIS extends IcsAsset {
        | automatedCollection
            developer info: "T802"
            -> networks.assets.access
        & defaultCredentials
            developer info: "T812"
            -> authenticatedAccess
        & modifyParameter {I}
            developer info: "T836"
            -> physicalImpact
        | physicalImpact
            developer info: "T836"
        | hidableAttacks
        & modifyAlarmSettings //lowProbability
            developer info: "T838"
            -> hidableAttacks
        | dontModifyAlarmSettings //highProbabilty
            developer info: "T838"
            -> hidableAttacks
        # alarmSettingsNotModified
            developer info: "T838"
            -> modifyAlarmSettings
        & monitorProcessState
            developer info: "T801"
        & remoteSystemDiscovery
            developer info: "T846"
            -> networks.assets.access

        # regularAuditOfSystem
            developer info: "T838,T801 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState
        # applyPrincipleOfLeastPrivilege
            developer info: "T812,T838,T801 - Mitigation"
            -> defaultCredentials,
                modifyAlarmSettings,
                monitorProcessState
        # applyPasswordRules
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # applyMultiFactorAuthentication
            developer info: "T812 - Mitigation"
            -> defaultCredentials
        # restrictedPhysicalAccess
            developer info: "T838,T801,T846 - Mitigation"
            -> modifyAlarmSettings,
                monitorProcessState,
                remoteSystemDiscovery
        # applyIDS
            developer info: "T838,T846 - Mitigation"
            -> modifyAlarmSettings,
                remoteSystemDiscovery
    }
}

category Networking {
    asset IcsNetwork {
        | access
            -> MITM
        & MITM
            developer info: "T830"
            -> blockReportingMessage,
                modifyParameter,
                unauthorizedCommandMessage,
                spoofReportingMessage
        | blockReportingMessage
            -> assets[Controller].blockReportingMessage,
                assets[IOServer].blockReportingMessage
        | modifyParameter
            -> assets[ControlServer].modifyParameter,
                assets[Controller].modifyParameter,
                assets[SIS].modifyParameter,
                assets[HMI].modifyParameter
        | unauthorizedCommandMessage
            -> assets[Controller].unauthorizedCommandMessage
        | spoofReportingMessage
            -> assets[ControlServer].spoofReportingMessage

        # encryptCommunication
            developer info: "T830,T848,T850,T856 - Mitigation"
            -> MITM,
                assets[ControlServer].rogueMasterDevice,
                assets[HMI].rogueMasterDevice,
                assets[EngineeringWorkstation].rogueMasterDevice,
                assets[ControlServer].roleIdentification,
                assets[HMI].roleIdentification,
                assets[DataHistorian].roleIdentification,
                assets[Controller].roleIdentification,
                assets[ControlServer].spoofReportingMessage
        # challengeResponseAuthentication
            developer info: "T830,T859 - Mitigation"
            -> MITM,
                assets.validAccounts
        # restrictedPhysicalAccess
            developer info: "T830- Mitigation"
            -> MITM
        # applyIDS
            developer info: "T830,T859 - Mitigation"
            -> MITM,
                assets.validAccounts
        # whitelistAllowedDevices
            -> assets[ControlServer].rogueMasterDevice,
                assets[HMI].rogueMasterDevice,
                assets[EngineeringWorkstation].rogueMasterDevice
    }
}

category Zone {
    asset DMZ {

    }

    asset Internet {

    }
}

associations {
    IcsAsset [assets] * <-- NetworkAccess --> * [networks] IcsNetwork
    DataHistorian [historian] 1 <-- NetworkAccess --> * [dmz] DMZ
    Internet [internet] 1 <-- NetworkAccess --> * [dmz] DMZ
    IOServer [server] 1 <-- Controls --> * [controller] Controller
    AntiVirus [av] 1 <-- Monitors --> * [observed] IcsAsset
}